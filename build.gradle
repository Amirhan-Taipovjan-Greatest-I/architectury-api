plugins {
    id "architect-plugin" version "1.1.14"
    id "org.cadixdev.licenser" version "0.5.0"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven"
    id "maven-publish"
}

architect {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architect-plugin"
    apply plugin: "org.cadixdev.licenser"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version + "." + (System.getenv("GITHUB_RUN_NUMBER") == null ? "9999" : (System.getenv("GITHUB_RUN_NUMBER").toInteger() - Integer.parseInt(rootProject.last_minor_build)).toString())
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    license {
        header = rootProject.file("HEADER")

        ext {
            name = "shedaniel"
            year = 2020
        }

        exclude "**/NbtType.java"
        exclude "**/*.accessWidener"

        ignoreFailures = true
    }
}

task licenseFormatAll
subprojects { p -> licenseFormatAll.dependsOn("${p.path}:licenseFormat") }

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ["mavenCommon", "mavenFabric", "mavenForge"]
    publish = true
    pkg {
        repo = "cloth"
        name = "architectury"
        userOrg = "shedaniel"
        licenses = ["Apache-2.0"]
        version {
            vcsUrl = "https://github.com/shedaniel/architectury.git"
        }
    }
}